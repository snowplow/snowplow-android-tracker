apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'

gradle.projectsEvaluated {
    preBuild.dependsOn(generateVersion)
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

apply plugin: 'maven-publish'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 21
        versionCode 2
        versionName "$project.version"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main  {
            java {
                srcDir 'src/main/java'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services:6.5.87'

    // Jackson JSON processor
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1.1'


    // SquareUp
    compile 'com.squareup.okhttp:okhttp:2.1.0'
}

// build JAR file
task androidReleaseJar(type: Jar, dependsOn: assembleRelease) {
    from "$buildDir/intermediates/classes/release/"
}

task androidSourcesJar(type: Jar, dependsOn: assembleRelease) {
    from "$projectDir/src/main/java"
}

publishing {
    publications {
        maven(MavenPublication) {

            groupId "$project.group"
            version "$project.version"

            artifact androidReleaseJar

            artifact androidSourcesJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

task generateVersion {
    project.ext.set("outputDir",
            "$projectDir/src/main/java/com/snowplowanalytics/snowplow/tracker/generated")
    doFirst {
        println outputDir
        def srcFile = new File((String)outputDir, "Version.java")
        srcFile.parentFile.mkdirs()
        srcFile.write(
                """/*
 * Copyright (c) 2015 Snowplow Analytics Ltd. All rights reserved.
 *
 * This program is licensed to you under the Apache License Version 2.0,
 * and you may not use this file except in compliance with the Apache License Version 2.0.
 * You may obtain a copy of the Apache License Version 2.0 at http://www.apache.org/licenses/LICENSE-2.0.
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the Apache License Version 2.0 is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the Apache License Version 2.0 for the specific language governing permissions and limitations there under.
 */

package com.snowplowanalytics.snowplow.tracker.generated;

// DO NOT EDIT. AUTO-GENERATED.
public class Version {
    public static final String TRACKER = "andr-$project.version";
    public static final String VERSION = "$project.version";
}
""")
    }
}
