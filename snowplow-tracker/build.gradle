import org.jetbrains.dokka.gradle.DokkaTask

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

jacoco {
    toolVersion = "0.8.5"
}

project.ext {
    archLifecycleVersion = "2.2.0"
}

android {
    namespace "com.snowplowanalytics.snowplow.tracker"
    compileSdkVersion 35

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdkVersion 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "TRACKER_LABEL", "\"andr-$project.version\""
            buildConfigField "String", "TRACKER_VERSION", "\"$project.version\""
            enableUnitTestCoverage true
            enableAndroidTestCoverage true

        }
        release {
            buildConfigField "String", "TRACKER_LABEL", "\"andr-$project.version\""
            buildConfigField "String", "TRACKER_VERSION", "\"$project.version\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packaging {
        resources {
            excludes += [
                'LICENSE.txt',
                'META-INF/LICENSE',
                'META-INF/NOTICE',
                'META-INF/LICENSE.txt',
                'META-INF/NOTICE.txt',
                'META-INF/DEPENDENCIES'
            ]
        }
    }

    lint {
        disable 'InvalidPackage'
        abortOnError false
    }

    sourceSets {
        androidTest.manifest.srcFile "src/androidTest/AndroidManifest.xml"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    installation {
        installOptions '-d', '-t'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.preference:preference:1.2.1'
    compileOnly "androidx.lifecycle:lifecycle-extensions:$project.archLifecycleVersion"
    compileOnly "com.android.installreferrer:installreferrer:2.2"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup.okhttp3:okhttp:5.2.0'

    // test
    testImplementation "androidx.lifecycle:lifecycle-extensions:$project.archLifecycleVersion"
    testImplementation "com.android.installreferrer:installreferrer:2.2"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'com.google.android.gms:play-services-analytics:18.0.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver3:5.2.0'
}

repositories {
    mavenCentral()
}

coveralls {
    jacocoReportPath "$buildDir/reports/coverage/debug/report.xml"
}

tasks.withType(DokkaTask.class) {
    dokkaSourceSets {
        configureEach {
            includes.from(project.files(), "Module.md")

            perPackageOption {
                matchingRegex.set("com.snowplowanalytics.core.*") // exclude core packages from API docs
                suppress.set(true)
            }
        }
    }
}

def dokkaJar = tasks.register("dokkaJar", Jar) {
    dependsOn(tasks.named("dokkaHtml"))
    archiveClassifier.set("javadoc")
    from(tasks.named("dokkaHtml").flatMap { it.outputDirectory })
}

publishing {
    publications {
        register("release", MavenPublication) {
            println 'Publishing release:'
            println GROUP
            println POM_ARTIFACT_ID
            println VERSION_NAME

            groupId GROUP
            artifactId POM_ARTIFACT_ID
            version VERSION_NAME

            // Includes the AAR and the sources jar generated by withSourcesJar()

            // Attach the Dokka "javadoc" jar
            artifact(dokkaJar)

            pom {
                name = POM_NAME
                packaging = POM_PACKAGING
                description = POM_DESCRIPTION
                url = POM_URL

                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                        distribution = POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                    }
                }

                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
}

// Defer wiring the Android 'release' component until it's created by AGP
afterEvaluate {
    def releaseComponent = components.findByName("release")
    if (releaseComponent == null) {
        println "No 'release' component found. Available components: ${components*.name}"
    } else {
        publishing.publications.named("release", MavenPublication) {
            from(releaseComponent)
        }
    }
}

signing {
    if (System.getenv('ORG_GRADLE_PROJECT_signingKey') && System.getenv('ORG_GRADLE_PROJECT_signingPassword')) {
        println 'Found signing credentials. Signing...'
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign(publishing.publications["release"])
        println 'Used useInMemoryPgpKeys()'
    } else {
        println 'Signing credentials not found, skipping.'
    }
}
