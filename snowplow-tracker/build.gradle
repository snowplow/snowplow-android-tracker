plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = "0.7.7.201606060606"
}

project.ext {
    archLifecycleVersion = "1.1.1"
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    useLibrary 'android.test.base'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "$project.version"
    }

    buildTypes {
        debug {
            buildConfigField "String", "TRACKER_LABEL", "\"andr-$project.version\""
            buildConfigField "String", "TRACKER_VERSION", "\"$project.version\""

            testCoverageEnabled = true
        }
        release {
            buildConfigField "String", "TRACKER_LABEL", "\"andr-$project.version\""
            buildConfigField "String", "TRACKER_VERSION", "\"$project.version\""

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }
}

dependencies {
    implementation "android.arch.lifecycle:extensions:$project.archLifecycleVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    androidTestImplementation 'com.google.android.gms:play-services-analytics:16.0.1'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.4.1'
}

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        title = "Documentation for snowplow-android-core $version API"
        description "Generates Javadoc for $variant.name."
        source = variant.getJavaCompileProvider().get().source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        doFirst { classpath = files(variant.javaCompiler.classpath.files) + files(ext.androidJar) }
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        exclude '**/R.java'
    }
}

coveralls {
    jacocoReportPath "$buildDir/reports/coverage/debug/report.xml"
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'snowplow-android-tracker'
        pom.version = "$project.version"
        pom.project {
            name = 'snowplow-android-tracker'
            description = 'Snowplow event tracker for Android 14+. Add analytics to your apps and games.'
            url = 'https://github.com/snowplow/snowplow-android-tracker'
            inceptionYear = '2014'

            packaging = 'aar'
            groupId = 'com.snowplowanalytics'

            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            scm {
                connection = 'https://github.com/snowplow/snowplow-android-tracker.git'
                url = 'https://github.com/snowplow/snowplow-android-tracker'
            }
            developers {
                developer {
                    name = 'Joshua Beemster'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_SNOWPLOW_MAVEN_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_SNOWPLOW_MAVEN_API_KEY')

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'snowplow-maven'
        name = 'snowplow-android-tracker'
        group = 'com.snowplowanalytics'
        userOrg = 'snowplow'

        websiteUrl = 'https://github.com/snowplow/snowplow-android-tracker'
        vcsUrl = 'https://github.com/snowplow/snowplow-android-tracker'
        issueTrackerUrl = 'https://github.com/snowplow/snowplow-android-tracker/issues'

        licenses = ['Apache-2.0']

        version.name = "$project.version"
    }
}
