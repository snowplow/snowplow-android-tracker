apply plugin: 'com.android.application'

project.ext {
    archLifecycleVersion = "2.2.0"
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.snowplowanalytics.snowplowtrackerdemo"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 3
        versionName "0.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            String MICRO_SUBDOMAIN = System.getenv("MICRO_SUBDOMAIN")

            // it is null if it runs locally behind serveo, otherwise it is travis, running behind ngrok
            if (MICRO_SUBDOMAIN == null) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                MICRO_SUBDOMAIN = properties.getProperty('microSubdomain')?:"NoMicroSubdomainFound"
                buildConfigField "String", "MICRO_ENDPOINT", "\"".concat(MICRO_SUBDOMAIN).concat("\"")
            } else { // travis case
                buildConfigField "String", "MICRO_ENDPOINT", "\"".concat(MICRO_SUBDOMAIN).concat(".ngrok.io").concat("\"")
            }

            testCoverageEnabled = true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "snowplow-demo-app-${output.baseName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.lifecycle:lifecycle-extensions:$project.archLifecycleVersion"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':snowplow-android-tracker')
    implementation "androidx.browser:browser:1.2.0"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.preference:preference:1.1.0"
    implementation 'com.squareup.okhttp3:okhttp:3.14.6'
    implementation 'com.google.code.gson:gson:2.8.5'
    // test
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
}
